#!/usr/local/bin/perl
# -*- coding: utf-8; mode:cperl -*-

package MilterOption;

use Moose;
use Moose::Util::TypeConstraints;
with 'MooseX::Getopt';

foreach my $dir ( qw( RunDirectory LogDirectory ) ) {
    subtype "ManagerOption::$dir",
	as 'Str',
	where { -d $_ },
	message { qq{$dir directory "$_" must exist.} };
}

foreach my $value ( qw( MaxChildren MaxRequests Threshold Period MaxMessages) ) {
    subtype "ManagerOption::$value",
	as 'Int',
        where { $_ >= 0 },
	message { "$value must be plus integer." };
}

has 'rundir'          => ( isa => 'ManagerOption::RunDirectory', is => 'ro', default => '/var/run/smtpauth' );
has 'logdir'          => ( isa => 'ManagerOption::LogDirectory', is => 'ro', default => '/var/log/smtpauth' );
has 'max_children'    => ( isa => 'ManagerOption::MaxChildren',  is => 'ro', default => 5 );
has 'max_requests'    => ( isa => 'ManagerOption::MaxRequests',  is => 'ro', default => 1000 );
has 'threshold'       => ( isa => 'ManagerOption::Threshold',    is => 'ro', default => 60 );
has 'period'          => ( isa => 'ManagerOption::Period',       is => 'ro', default => 120 );
has 'max_messages'    => ( isa => 'ManagerOption::MaxMessages',  is => 'ro', default => 10_000 );
has 'foreground'      => ( isa => 'Bool', is => 'ro', default => 0 );
has 'auto_reject'     => ( isa => 'Bool', is => 'ro', default => 0 );
has 'alert_email'     => ( isa => 'Bool', is => 'ro', default => 0 );
has 'alert_mailhost'  => ( isa => 'Str',  is => 'ro', default => 'loaclhost' );
has 'alert_port'      => ( isa => 'Int',  is => 'ro', default => 25 );
has 'alert_sender'    => ( isa => 'Str',  is => 'ro', default => 'postmaster@localhost.localdomain' );
has 'alert_recipient' => ( isa => 'ArrayRef[Str]', is => 'ro', default => sub { [ 'postmaster@localhost.localdomain' ] } );
has 'geoip_v4'        => ( isa => 'Str',  is => 'ro' );
has 'geoip_v6'        => ( isa => 'Str',  is => 'ro' );

no Moose;
__PACKAGE__->meta->make_immutable;

package main;

use strict;
use warnings;
use English;
use POSIX;
use IO::File;
use Sys::Syslog;
use Milter::SMTPAuth;
use Milter::SMTPAuth::Utils;

my $option = MilterOption->new_with_options();

my $pid_file      = sprintf( "%s/pid",                     $option->rundir() );
my $filter_socket = sprintf( "unix:%s/filter.sock",        $option->rundir() );
my $logger_socket = sprintf( "unix:%s/log-collector.sock", $option->rundir() );
my $log_file      = sprintf( "%s/stats.log",               $option->logdir() );

openlog( 'smtpauth-manager', 'pid,nowait', 'mail' );
syslog( 'info', 'starting manager' );


my @log_collector_arguments = (
    '--recv_address', $logger_socket,
    '--log',          $log_file,
    '--threshold',    $option->threshold(),
    '--period',       $option->period(),
    '--max_messages', $option->max_messages(),
    '--foreground',
);
if ( $option->auto_reject() ) {
    push( @log_collector_arguments, '--auto_reject' );
}
if ( $option->geoip_v4() ) {
    push( @log_collector_arguments, '--geoip_v4', $option->geoip_v4() );
}
if ( $option->geoip_v6() ) {
    push( @log_collector_arguments, '--geoip_v6', $option->geoip_v6() );
}
if ( $option->alert_email() ) {
    push( @log_collector_arguments,
	  '--alert_email',
	  '--alert_mailhost',  $option->alert_mailhost(),
	  '--alert_port',      $option->alert_port(),
	  '--alert_sender',    $option->alert_sender() );
    foreach my $recipient ( @{ $option->alert_recipient() } ) {
	push( @log_collector_arguments, '--alert_recipient', $recipient );
    }
}


my @filter_arguments = (
    '--listen_address', $filter_socket,
    '--logger_address', $logger_socket,
    '--max_children',   $option->max_children(),
    '--max_requests',   $option->max_requests(),
    '--foreground',
);

my $manager = new Milter::SMTPAuth(
    processes => [
        new Milter::SMTPAuth::Child(
            command     => 'smtpauth-log-collector',
            arguments   => \@log_collector_arguments,
            signal_stop => 10,          # SIGUSR1
        ),
        new Milter::SMTPAuth::Child(
            command   => 'smtpauth-filter',
            arguments => \@filter_arguments,
        ),
    ]
);

if ( ! $option->foreground() ) {
    Milter::SMTPAuth::Utils::daemonize( $pid_file );
}


$SIG{TERM} = $SIG{INT} = sub {
    $manager->stop();
    if ( -f $pid_file ) {
	unlink( $pid_file );
    }
};

$manager->start();
